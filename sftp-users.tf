# https://learn.microsoft.com/en-us/azure/storage/blobs/secure-file-transfer-protocol-support#supported-algorithms
resource "tls_private_key" "sftp_users_keys" {
  for_each = local.sftp_users

  algorithm   = "RSA"
  rsa_bits =  "4096"
}

resource "azurerm_storage_account_local_user" "sftp_users" {
  for_each = local.sftp_users

  name = each.key

  storage_account_id = data.terraform_remote_state.sftp-storage.outputs.test-sftp-storage-account-id

  ssh_key_enabled      = each.value.ssh_key_enabled
  ssh_password_enabled = each.value.ssh_password_enabled

  # The first container in the `permissions_scopes` list will always be the default home directory
  home_directory = coalesce(each.value.home_directory, each.value.permissions_scopes[0].target_container)

  # https://learn.microsoft.com/en-us/azure/storage/blobs/secure-file-transfer-protocol-support#container-permissions
  dynamic "permission_scope" {
    for_each = each.value.permissions_scopes
    content {
      service       = "blob"
      resource_name = permission_scope.value.target_container
      permissions {
        create = contains(permission_scope.value.permissions, "All") || contains(permission_scope.value.permissions, "Create")
        delete = contains(permission_scope.value.permissions, "All") || contains(permission_scope.value.permissions, "Delete")
        list   = contains(permission_scope.value.permissions, "All") || contains(permission_scope.value.permissions, "List")
        read   = contains(permission_scope.value.permissions, "All") || contains(permission_scope.value.permissions, "Read")
        write  = contains(permission_scope.value.permissions, "All") || contains(permission_scope.value.permissions, "Write")
      }
    }
  }

  dynamic "ssh_authorized_key" {
    for_each = each.value.ssh_key_enabled ? ["auto"] : []
    content {
      key         = tls_private_key.sftp_users_keys[each.key].public_key_openssh
      description = "Automatically generated by Terraform"
    }
  }

  dynamic "ssh_authorized_key" {
    for_each = each.value.ssh_key_enabled ? each.value.ssh_authorized_keys : []
    content {
      key         = ssh_authorized_key.value.key
      description = ssh_authorized_key.value.description
    }
  }

  lifecycle {
    #precondition {
     # condition = alltrue([
      #  for scope in each.value.permissions_scopes : contains(keys(var.containers), scope.target_container)
      #])
      #error_message = format("At least one target container does not exist (or is being deleted) for user %s.", each.key)
    #}
    precondition {
      condition = alltrue(flatten([
        for scope in each.value.permissions_scopes : [
          for permission in scope.permissions : contains(local.sftp_users_permissions, permission)
        ]
      ]))
      error_message = format("One or more permissions are wrong for user %s. Allowed values in the list are: %s.", each.key, join(", ", [
        for permission in local.sftp_users_permissions : "'${permission}'"
      ]))
    }
    # Required because otherwise Terraform will apply successfully but the SFTP connection will fail when using the default SFTP connection command line
    postcondition {
      condition     = contains(self.permission_scope[*].resource_name, split("/", self.home_directory)[0])
      error_message = format("The home directory of user %s does not refer to any container in its permissions scopes.", self.name)
    }
  }
}




resource "azurerm_key_vault_secret" "secret" {
  for_each = local.sftp_users
  name = "secret-${each.key}"
  value = coalesce(tls_private_key.sftp_users_keys[each.key].private_key_pem, azurerm_storage_account_local_user.sftp_users[each.key].password)
  content_type = "Private PEM"
  key_vault_id = "/subscriptions/50a209a2-959f-4cbb-a46a-9d158876e328/resourceGroups/rg-sftp/providers/Microsoft.KeyVault/vaults/bodgedcloud-sftp-kv"
  
}

resource "azurerm_key_vault_secret" "password" {
  for_each = local.sftp_users
  name = "pass-${each.key}"
  value = azurerm_storage_account_local_user.sftp_users[each.key].password
  content_type = "Password"
  key_vault_id = "/subscriptions/50a209a2-959f-4cbb-a46a-9d158876e328/resourceGroups/rg-sftp/providers/Microsoft.KeyVault/vaults/bodgedcloud-sftp-kv"
  
}
